// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/RAM64.hdl
/**
 * Memory of sixty four 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    // Split: address[5..3] selects which RAM8, address[2..0] selects register within that RAM8
    DMux8Way(in=load, sel=address[3..5], a=la, b=lb, c=lc, d=ld, e=le, f=lf, g=lg, h=lh);

    RAM8(in=in, load=la, address=address[0..2], out=outA);
    RAM8(in=in, load=lb, address=address[0..2], out=outB);
    RAM8(in=in, load=lc, address=address[0..2], out=outC);
    RAM8(in=in, load=ld, address=address[0..2], out=outD);
    RAM8(in=in, load=le, address=address[0..2], out=outE);
    RAM8(in=in, load=lf, address=address[0..2], out=outF);
    RAM8(in=in, load=lg, address=address[0..2], out=outG);
    RAM8(in=in, load=lh, address=address[0..2], out=outH);

    Mux8Way16(a=outA, b=outB, c=outC, d=outD, e=outE, f=outF, g=outG, h=outH,
              sel=address[3..5], out=out);
}